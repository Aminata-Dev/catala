

```catala-metadata
declaration structure Individual:
  data income content integer
  data number_of_children content integer
  
```

```catala-metadata
declaration scope IncomeTaxComputation:
  context individual content Individual

  context breakpoint content integer
  context rate1 content integer
  context rate2 content integer

  context income_tax content integer
```


#### Article 1

The tax amount for a two-brackets computation is equal to the amount
of income in each bracket multiplied by the rate of each bracket.

```catala
scope IncomeTaxComputation :
  label base definition income_tax equals
    if individual.income <= breakpoint then
      individual.income * rate1 / 100
    else (
      (breakpoint * rate1 +
      (individual.income - breakpoint) * rate2) / 100
    ) 
```

#### Article 2

For individuals whose income is greater than $100,000, the income
tax of article 1 is 40 of the income above $100,000. Below $100,000, the
income tax is 20 of the income.

```catala
scope IncomeTaxComputation:
  definition breakpoint equals 100000
  label base_rate definition rate1 equals 20 
  definition rate2 equals 40 
  # Writing 20 is just an abbreviation for 0.20
```

#### Article 3

If the individual is in charge of 2 or more children, then
the income tax for the lower bracket mentioned at article 1 is 
equal to 15 .

```catala
scope IncomeTaxComputation:
  exception base_rate definition rate1 under condition
    individual.number_of_children >= 2
  consequence equals 15 
  # Writing 15 is just an abbreviation for 0.15
```

#### Article 4

Individuals earning less than $10,000 are exempted of the income tax mentioned
at article 1.

```catala
scope IncomeTaxComputation:
  exception base definition income_tax under condition
    individual.income <= 10000
  consequence equals 0
```

#### Test 1

Dummy individual.

```catala
scope IncomeTaxComputation:
  definition individual equals Individual {
    --income: 4000
    --number_of_children: 0
  }
```


### Monotony meta-property

Commented-out for now.


  ```catala-metadata
  declaration scope CheckMonotony:
    context a content IncomeTaxComputation
    context b content IncomeTaxComputation
  ```


  ```catala
  scope CheckMonotony:
    assumption a.individual.income < b.individual.income
    assumption a.number_of_children == b.number_of_children

    assertion a.income_tax < b.income_tax
  ```